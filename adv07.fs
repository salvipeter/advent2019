create program
  3 , 8 , 1001 , 8 , 10 , 8 , 105 , 1 , 0 , 0 , 21 , 46 , 55 , 72 , 85 , 110 ,
  191 , 272 , 353 , 434 , 99999 , 3 , 9 , 1002 , 9 , 5 , 9 , 1001 , 9 , 2 , 9 ,
  102 , 3 , 9 , 9 , 101 , 2 , 9 , 9 , 102 , 4 , 9 , 9 , 4 , 9 , 99 , 3 , 9 ,
  102 , 5 , 9 , 9 , 4 , 9 , 99 , 3 , 9 , 1002 , 9 , 2 , 9 , 101 , 2 , 9 , 9 ,
  1002 , 9 , 2 , 9 , 4 , 9 , 99 , 3 , 9 , 1002 , 9 , 4 , 9 , 101 , 3 , 9 , 9 ,
  4 , 9 , 99 , 3 , 9 , 1002 , 9 , 3 , 9 , 101 , 5 , 9 , 9 , 1002 , 9 , 3 , 9 ,
  101 , 3 , 9 , 9 , 1002 , 9 , 5 , 9 , 4 , 9 , 99 , 3 , 9 , 1001 , 9 , 2 , 9 ,
  4 , 9 , 3 , 9 , 101 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 101 , 2 , 9 , 9 , 4 , 9 ,
  3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 ,
  102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 1002 , 9 ,
  2 , 9 , 4 , 9 , 3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 1002 , 9 , 2 , 9 ,
  4 , 9 , 99 , 3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 102 , 2 , 9 , 9 , 4 ,
  9 , 3 , 9 , 1001 , 9 , 1 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 1 , 9 , 4 , 9 , 3 ,
  9 , 1002 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 ,
  102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 101 , 1 , 9 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 ,
  1 , 9 , 4 , 9 , 3 , 9 , 101 , 2 , 9 , 9 , 4 , 9 , 99 , 3 , 9 , 1002 , 9 , 2 ,
  9 , 4 , 9 , 3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 101 , 2 , 9 , 9 , 4 ,
  9 , 3 , 9 , 1001 , 9 , 1 , 9 , 4 , 9 , 3 , 9 , 1002 , 9 , 2 , 9 , 4 , 9 , 3 ,
  9 , 1001 , 9 , 1 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 1 , 9 , 4 , 9 , 3 , 9 ,
  1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 1 , 9 , 4 , 9 , 3 , 9 , 101 ,
  2 , 9 , 9 , 4 , 9 , 99 , 3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 102 , 2 ,
  9 , 9 , 4 , 9 , 3 , 9 , 101 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 2 , 9 ,
  4 , 9 , 3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 1002 , 9 , 2 , 9 , 4 , 9 ,
  3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 1002 , 9 , 2 , 9 , 4 , 9 , 3 , 9 ,
  1002 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 1 , 9 , 4 , 9 , 99 , 3 , 9 ,
  101 , 1 , 9 , 9 , 4 , 9 , 3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 102 , 2 ,
  9 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 1002 , 9 , 2 , 9 ,
  4 , 9 , 3 , 9 , 1002 , 9 , 2 , 9 , 4 , 9 , 3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 ,
  3 , 9 , 102 , 2 , 9 , 9 , 4 , 9 , 3 , 9 , 1001 , 9 , 2 , 9 , 4 , 9 , 3 , 9 ,
  1001 , 9 , 2 , 9 , 4 , 9 , 99 ,
here program - cell / constant length

variable phase
variable i/o
create memory 1000 cells allot
: mem@ ( ip -- x ) cells memory + @ ;
: mem! ( ip x -- ) swap cells memory + ! ;
\ ----------------------
\ New: saving mechanism
variable slot
create memsave 5000 cells allot
create ipsave 5 cells allot
: save ( ip -- )
  ipsave slot @ cells + !
  memory memsave slot @ 1000 cells * + 1000 cells move ;
: load ( -- ip )
  memsave slot @ 1000 cells * + memory 1000 cells move
  ipsave slot @ cells + @ ;
\ ----------------------
: init ( -- ) program memory length cells move ;
: arg ( ip -- ip ip+1 ) dup 1+ ;
: [arg] ( ip -- [ip] ip+1 ) dup mem@ swap 1+ ;
: args ( ip m k -- a1 ... ak ip' )
  rot 1+ -rot 0 ?do
    10 /mod >r 0= if [arg] else arg then r>
  loop drop ;
\ Instructions ( ip m -- ip' )
: add 3 args >r -rot mem@ swap mem@ + mem! r> ;
: mul 3 args >r -rot mem@ swap mem@ * mem! r> ;
: input 1 args >r phase @ 0< if i/o @ else phase @ -1 phase ! then mem! r> ;
: output 1 args >r mem@ i/o ! r> ;
: jmptrue 2 args rot mem@ 0<> if drop mem@ else nip then ;
: jmpfalse 2 args rot mem@ 0= if drop mem@ else nip then ;
: less 3 args >r -rot mem@ swap mem@ > abs mem! r> ;
: equals 3 args >r -rot mem@ swap mem@ = abs mem! r> ;
create instr
  ' add , ' mul , ' input , ' output , ' jmptrue , ' jmpfalse ,
  ' less , ' equals ,
: call ( ip m n -- ip' ) 1- cells instr + @ execute ;
: run ( n -- )
  begin
    dup mem@ 100 /mod swap
    dup 99 <> while
    dup 8 > abort" Invalid opcode"
    dup >r call
    r> 4 = if save exit then \ save & exit after output
  repeat 2drop save ;

\ Generate all permutations of 0,1,2,3,4
create phases 0 , 1 , 2 , 3 , 4 ,
: p@ cells phases + @ ;
: p! cells phases + ! ;
: change ( i j -- ) 2dup p@ swap p@ rot p! swap p! ;
: permutations ( n -- )
  ?dup 0= if 5 0 do i p@ , loop exit then
  dup 1- swap 0 ?do
    dup i change
    dup recurse
    dup i change
  loop drop ;
create phases 5 permutations

: try ( addr -- n )
  0 i/o ! 5 0 do
    dup i cells + @ phase ! init 0 run
  loop drop i/o @ ;
: part1 0 120 0 do i 5 * cells phases + try max loop . ;

: try ( addr -- n )
  0 i/o ! true begin \ addr first-round?
    5 0 do
      i slot !
      dup if over i cells + @ 5 + phase ! init 0 else load then run 
    loop drop false
    ipsave @ cells memsave + @ 99 = if 2drop i/o @ exit then
  again ;
: part2 0 120 0 do i 5 * cells phases + try max loop . ;
